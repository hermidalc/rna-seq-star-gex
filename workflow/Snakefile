import re
from os import getcwd, walk
from os.path import dirname, exists, join
from pathlib import Path
from shutil import rmtree

import pandas as pd
from snakemake.utils import validate


CONFIG_DIR = "config"
RESOURCES_DIR = "resources"
RESULTS_DIR = "results"
LOG_DIR = "logs"
RULES_DIR = "rules"


configfile: "config/config.yaml"


STUDY_NAME = config["study"]["name"]
SAMPLE_CONFIG_FILE = config["study"]["samples"]
UNIT_CONFIG_FILE = config["study"]["units"]

TEMP_DIR = config["tmp_dir"]

SAMPLE_DF = pd.read_csv(
    SAMPLE_CONFIG_FILE, sep="\t", dtype={"sample_name": str, "sample_label": str}
).set_index("sample_name", drop=False, verify_integrity=True)
validate(SAMPLE_DF, schema="schemas/samples.schema.yaml")

UNIT_DF = pd.read_csv(
    UNIT_CONFIG_FILE, sep="\t", dtype={"sample_name": str, "unit_name": str}
)
validate(UNIT_DF, schema="schemas/units.schema.yaml")

if UNIT_DF["unit_name"].isna().all():
    UNIT_DF = UNIT_DF.set_index("sample_name", drop=False, verify_integrity=True)
    RUN_ID_WILDCARD_STR = "{sample}"
    SAMPLES = UNIT_DF["sample_name"].tolist()
    EXPAND_PARAMS = {"sample": SAMPLES}
else:
    UNIT_DF.set_index(["sample_name", "unit_name"], drop=False, verify_integrity=True)
    RUN_ID_WILDCARD_STR = "{sample}_{unit}"
    SAMPLES = UNIT_DF["sample_name"].tolist()
    UNITS = UNIT_DF["unit_name"].tolist()
    EXPAND_PARAMS = {"sample": SAMPLES, "unit": UNITS}

SAMPLE_DF = SAMPLE_DF.loc[SAMPLES]

SAMPLE_LABELS = [
    n if pd.isna(l) else l
    for n, l in zip(SAMPLE_DF["sample_name"], SAMPLE_DF["sample_label"])
]

EXPAND_PARAMS["sub_dir"] = [Path(dirname(fq)).name for fq in UNIT_DF["fq1"]]

GENCODE_RESOURCES_DIR = join(RESOURCES_DIR, "gencode")

GENCODE_PROTOCOL = config["gencode"]["protocol"]
GENCODE_SPECIES = config["gencode"]["species"]
GENCODE_RELEASE = config["gencode"]["release"]
GENCODE_BUILD = config["gencode"]["build"]
GENCODE_REGIONS = config["gencode"]["regions"]
GENCODE_ANNOT_FMT = config["gencode"]["annot"]["fmt"]

GENCODE_GENOME_NAME = f"gencode_{GENCODE_RELEASE}_{GENCODE_BUILD}"

GENCODE_GENOME_SEQ_FILE = join(GENCODE_RESOURCES_DIR, f"{GENCODE_GENOME_NAME}.fa")
GENCODE_GENOME_ANNOT_FILE = join(
    GENCODE_RESOURCES_DIR, f"{GENCODE_GENOME_NAME}.{GENCODE_ANNOT_FMT.lower()}"
)

GENCODE_GENOME_SEQ_WRAPPER = join(
    config["wrapper"]["base_url"], "bio/reference/gencode/sequence"
)
GENCODE_GENOME_ANNOT_WRAPPER = join(
    config["wrapper"]["base_url"], "bio/reference/gencode/annotation"
)

GENCODE_GENE_ANNOT_FILE = join(
    GENCODE_RESOURCES_DIR, f"{GENCODE_GENOME_NAME}_gene_annot.tsv"
)
GENCODE_LOG_DIR = join(LOG_DIR, "gencode")
GENCODE_GENE_ANNOT_LOG = join(GENCODE_LOG_DIR, f"{GENCODE_GENOME_NAME}_gene_annot.log")

GENCODE_GENE_ANNOT_WRAPPER = join(
    config["wrapper"]["base_url"], "bio/reference/gtf/gene_annot"
)

RSEQC_RESOURCES_DIR = join(RESOURCES_DIR, "rseqc")
RSEQC_LOG_DIR = join(LOG_DIR, "rseqc")

RSEQC_GENOME_ANNOT_FILE = join(RSEQC_RESOURCES_DIR, f"{GENCODE_GENOME_NAME}.bed")
RSEQC_GENOME_ANNOT_DB = join(RSEQC_RESOURCES_DIR, f"{GENCODE_GENOME_NAME}.db")

RSEQC_ANNOT_LOG = join(RSEQC_LOG_DIR, "gtf2bed.log")

RSEQC_ANNOT_WRAPPER = join(config["wrapper"]["base_url"], "bio/gffutils/gtf2bed")

FASTQ_DATA_DIR = config["fastq"]["data_dir"]
FASTQ_PAIRS = config["fastq"]["pairs"]
FASTQ_EXT = config["fastq"]["ext"]
FASTQ_PLATFORM = config["fastq"]["platform"]

TRIMMED_RESULTS_DIR = join(RESULTS_DIR, "fastp", "{sub_dir}")
FASTP_LOG_DIR = join(LOG_DIR, "fastp", "{sub_dir}")

FASTQ1_FILE = join(
    FASTQ_DATA_DIR, f"{RUN_ID_WILDCARD_STR}_{FASTQ_PAIRS[0]}.{FASTQ_EXT}"
)
FASTQ2_FILE = join(
    FASTQ_DATA_DIR, f"{RUN_ID_WILDCARD_STR}_{FASTQ_PAIRS[1]}.{FASTQ_EXT}"
)
TRIMMED_FASTQ1_FILE = join(
    TRIMMED_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_{FASTQ_PAIRS[0]}.{FASTQ_EXT}"
)
TRIMMED_FASTQ2_FILE = join(
    TRIMMED_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_{FASTQ_PAIRS[1]}.{FASTQ_EXT}"
)

TRIMMED_UNPAIR1_FILE = join(
    TRIMMED_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_U1.{FASTQ_EXT}"
)
TRIMMED_UNPAIR2_FILE = join(
    TRIMMED_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_U2.{FASTQ_EXT}"
)
FAILED_READS_FILE = join(
    TRIMMED_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}.failed.{FASTQ_EXT}"
)
FASTP_HTML_REPORT_FILE = join(TRIMMED_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_report.html")
FASTP_JSON_REPORT_FILE = join(TRIMMED_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_report.json")

FASTP_LOG = join(FASTP_LOG_DIR, f"{RUN_ID_WILDCARD_STR}.log")
FASTP_WRAPPER = join(config["wrapper"]["base_url"], "bio/fastp")

READ_LENGTH_RESULTS_DIR = join(RESULTS_DIR, "read_length", "{sub_dir}")
READ_LENGTH_LOG_DIR = join(LOG_DIR, "read_length", "{sub_dir}")

READ_LENGTH_HISTOGRAM_FILE = join(
    READ_LENGTH_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_histogram.tsv"
)
READ_LENGTH_FILE = join(READ_LENGTH_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_length.txt")

READ_LENGTH_HISTOGRAM_LOG = join(
    READ_LENGTH_LOG_DIR, f"{RUN_ID_WILDCARD_STR}_histogram.log"
)
READ_LENGTH_LOG = join(READ_LENGTH_LOG_DIR, f"{RUN_ID_WILDCARD_STR}_length.log")

READ_LENGTH_WRAPPER = join(config["wrapper"]["base_url"], "bio/bbmap/readlength")

STAR_RESOURCES_DIR = join(RESOURCES_DIR, "star")
STAR_RESULTS_DIR = join(RESULTS_DIR, "star", "{sub_dir}")
STAR_LOG_DIR = join(LOG_DIR, "star", "{sub_dir}")
STAR_GENOME_DIR = join(STAR_RESOURCES_DIR, GENCODE_GENOME_NAME)
STAR_OUTPUT_DIR = join(STAR_RESULTS_DIR, RUN_ID_WILDCARD_STR)
STAR_PASS1_OUTPUT_DIR = join(STAR_OUTPUT_DIR, "_STARpass1")
STAR_PASS2_OUTPUT_DIR = join(STAR_OUTPUT_DIR, "_STARpass2")
STAR_OUTPUT_LOG_DIR = join(STAR_LOG_DIR, RUN_ID_WILDCARD_STR)

STAR_PASS1_SJ_FILE = join(STAR_PASS1_OUTPUT_DIR, "SJ.out.tab")
STAR_PASS1_SJ_FILTERED_FILE = join(STAR_PASS1_OUTPUT_DIR, "SJ.filtered.out.tab")

STAR_BAM_FILE = join(
    STAR_PASS2_OUTPUT_DIR,
    "Aligned.sortedByCoord.out.bam"
    if config["star"]["align"]["sort_bam"]
    else "Aligned.out.bam",
)
STAR_READ_COUNT_FILE = join(STAR_PASS2_OUTPUT_DIR, "ReadsPerGene.out.tab")

SAM_ATTR_RG_LINE = (
    f"ID:{RUN_ID_WILDCARD_STR} PL:{FASTQ_PLATFORM} " f"SM:{RUN_ID_WILDCARD_STR} LB:RNA"
)
STAR_BAM_SORT = (
    "SortedByCoordinate" if config["star"]["align"]["sort_bam"] else "Unsorted"
)

STAR_LOG_DIR = join(LOG_DIR, "star")
STAR_GENOME_LOG = join(
    STAR_LOG_DIR, GENCODE_GENOME_NAME, f"{GENCODE_GENOME_NAME}_star_index.log"
)
STAR_PASS1_SJ_FILTERED_LOG = join(
    STAR_OUTPUT_LOG_DIR, f"{RUN_ID_WILDCARD_STR}_star_filter_pass1_sj.log"
)
STAR_ALIGN_PASS1_LOG = join(
    STAR_OUTPUT_LOG_DIR, f"{RUN_ID_WILDCARD_STR}_star_pass1.log"
)
STAR_ALIGN_PASS2_LOG = join(
    STAR_OUTPUT_LOG_DIR, f"{RUN_ID_WILDCARD_STR}_star_pass2.log"
)

STAR_GENOME_WRAPPER = join(config["wrapper"]["base_url"], "bio/star/index")
STAR_ALIGN_WRAPPER = join(config["wrapper"]["base_url"], "bio/star/align")

RSEQC_RESULTS_DIR = join(RESULTS_DIR, "rseqc", "{sub_dir}")
RSEQC_LOG_DIR = join(LOG_DIR, "rseqc", "{sub_dir}")

RSEQC_INFER_EXPERIMENT_FILE = join(
    RSEQC_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_infer_experiment.txt"
)
RSEQC_INFER_EXPERIMENT_LOG = join(
    RSEQC_LOG_DIR, f"{RUN_ID_WILDCARD_STR}_infer_experiment.log"
)

RSEQC_INFER_EXPERIMENT_WRAPPER = join(
    config["wrapper"]["base_url"], "bio/rseqc/infer_experiment"
)

RSEQC_STRAND_INFO_FILE = join(
    RSEQC_RESULTS_DIR, f"{RUN_ID_WILDCARD_STR}_strand_info.txt"
)

SUB_DIRS = list(set(EXPAND_PARAMS["sub_dir"]))
if len(SUB_DIRS) == 1:
    COUNTS_RESULTS_DIR = join(RESULTS_DIR, "counts", SUB_DIRS[0])
    COUNTS_LOG_DIR = join(LOG_DIR, "counts", SUB_DIRS[0])
else:
    COUNTS_RESULTS_DIR = join(RESULTS_DIR, "counts", STUDY_NAME)
    COUNTS_LOG_DIR = join(LOG_DIR, "counts", STUDY_NAME)

COUNT_MATRIX_FILE = join(COUNTS_RESULTS_DIR, f"{STUDY_NAME}_count_matrix.tsv")
COUNT_MATRIX_LOG = join(COUNTS_LOG_DIR, f"{STUDY_NAME}_count_matrix.log")

COUNT_ESET_FILE = join(COUNTS_RESULTS_DIR, f"{STUDY_NAME}_count_eset.rds")
COUNT_ESET_LOG = join(COUNTS_LOG_DIR, f"{STUDY_NAME}_count_eset.log")

COUNT_MATRIX_WRAPPER = join(config["wrapper"]["base_url"], "bio/star/count_matrix")

COUNT_ESET_WRAPPER = join(config["wrapper"]["base_url"], "bio/biobase/eset")


include: join(RULES_DIR, "common.smk")
include: join(RULES_DIR, "genome.smk")
include: join(RULES_DIR, "gene_annot.smk")
include: join(RULES_DIR, "trim.smk")
include: join(RULES_DIR, "read_length.smk")
include: join(RULES_DIR, "align.smk")
include: join(RULES_DIR, "strand_info.smk")
include: join(RULES_DIR, "count_matrix.smk")
include: join(RULES_DIR, "count_eset.smk")


wildcard_constraints:
    **{w: "|".join(set([re.escape(v) for v in l])) for w, l in EXPAND_PARAMS.items()},


rule all:
    input:
        GENCODE_GENOME_SEQ_FILE,
        GENCODE_GENOME_ANNOT_FILE,
        GENCODE_GENE_ANNOT_FILE,
        RSEQC_GENOME_ANNOT_FILE,
        STAR_GENOME_DIR,
        expand(TRIMMED_FASTQ1_FILE, zip, **EXPAND_PARAMS),
        expand(TRIMMED_FASTQ2_FILE, zip, **EXPAND_PARAMS),
        expand(READ_LENGTH_HISTOGRAM_FILE, zip, **EXPAND_PARAMS),
        expand(READ_LENGTH_FILE, zip, **EXPAND_PARAMS),
        expand(STAR_PASS1_SJ_FILE, zip, **EXPAND_PARAMS),
        expand(STAR_PASS1_SJ_FILTERED_FILE, zip, **EXPAND_PARAMS),
        expand(STAR_BAM_FILE, zip, **EXPAND_PARAMS),
        expand(STAR_READ_COUNT_FILE, zip, **EXPAND_PARAMS),
        expand(RSEQC_INFER_EXPERIMENT_FILE, zip, **EXPAND_PARAMS),
        expand(RSEQC_STRAND_INFO_FILE, zip, **EXPAND_PARAMS),
        COUNT_MATRIX_FILE,
        COUNT_ESET_FILE,


def clean(*dirs):
    for clean_dir in dirs:
        if exists(clean_dir):
            rmtree(clean_dir)
        for dirpath, dirnames, filenames in sorted(walk(getcwd())):
            for name in dirnames:
                if name == "__pycache__":
                    pycache_dir = join(dirpath, name)
                    if exists(pycache_dir):
                        rmtree(pycache_dir)


rule clean:
    run:
        clean(RESULTS_DIR, LOG_DIR)


rule clean_all:
    run:
        clean(RESOURCES_DIR, RESULTS_DIR, LOG_DIR)
